import uk.gov.hmcts.rse.AuthMode
import uk.gov.hmcts.rse.ManifestTask


plugins {
    id 'jacoco'
    id 'org.springframework.boot' version "${testSpringBootVersion}"
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'com.github.hmcts.rse-cft-lib'
    id 'uk.gov.hmcts.java' version '0.12.43'
    id 'io.freefair.lombok' version '8.13.1'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

repositories {
    mavenLocal()
}

def springBoot2 = testSpringBootVersion.startsWith("2")

dependencies {

    // Test this conflicting dependency doesn't break us on spring boot 3.
    cftlibImplementation 'javax.servlet:javax.servlet-api:4.0.1'

    // Enable live reload of just our application
    cftlibImplementation 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'
    // Presence of JWT related deps will activate the IdamAugmenter
    implementation 'com.auth0:java-jwt:4.4.0'
    implementation group: 'org.springframework.security', name: 'spring-security-oauth2-jose', version: '6.2.3'

    if (springBoot2) {
      // v2 idam java client needs this dependency but appears not to declare it correctly.
      implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.3'
    }
    cftlibTestImplementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'

    cftlibTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    cftlibTestImplementation 'org.springframework.security:spring-security-test'

    cftlibTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '3.0.2'
    cftlibTestImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    cftlibTestImplementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    cftlibTestImplementation 'org.awaitility:awaitility:4.2.0'
}

bootWithCCD {
    authMode = AuthMode.Local
    environment("RSE_LIB_STUB_AUTH_OUTBOUND", "true");
    // Force container recreation.
    environment('RSE_LIB_CLEAN_BOOT', 'true')
}

cftlibTest {
    environment("RSE_LIB_ADDITIONAL_DATABASES", "mydb")
    // Check that we are able to override the default port.
    environment 'RSE_LIB_S2S_PORT', '7431'
    environment 'RSE_LIB_DB_PORT', '8432'

    // Test environment variable substitution - this value should be subbed into one of our FixedLists
    environment 'CCD_DEF_CHANGED_NAME_HOW', '"ID": "ChangedNameHow"'
}

test {
    useJUnitPlatform()
}

tasks.each {
    if (it.name.contains("writeManifest")) {
        it.dependsOn(rootProject.tasks.publishToMavenLocal)
    }
}
tasks.test.dependsOn(bootWithCCD)
bootWithCCD.dependsOn(rootProject.tasks.publishToMavenLocal)
bootWithCCD.mustRunAfter cftlibTest

tasks.test.dependsOn(cftlibTest)
cftlibTest.dependsOn(rootProject.tasks.publishToMavenLocal)
cftlibTestClasses.dependsOn(rootProject.tasks.publishToMavenLocal)

task publishSecrets(type: Exec) {
    commandLine 'az', 'keyvault', 'secret', 'set', "--vault-name", "rse-cft-lib", "--name", "aat-env", "--file", layout.buildDirectory.file('cftlib/.aat-env').get().asFile.path
}

// Ensure we publish all required dependencies to maven local
tasks.compileCftlibJava.dependsOn rootProject.tasks.publishToMavenLocal
tasks.withType(ManifestTask).each {
    it.dependsOn rootProject.tasks.publishToMavenLocal
}
configurations.each {
    it.resolutionStrategy.useGlobalDependencySubstitutionRules = false
}
